buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        // the latest version of the android-apt plugin
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.dynamit.dagger2tests"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'


        // Load local.properties file and get api key information
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        def apiKey = properties.getProperty('api.key')

        buildConfigField "String", "API_URL", "\"https://swapi.co/api\""

        buildConfigField "String", "TMDB_API_URL", "\"https://api.themoviedb.org\""
        buildConfigField "String", "TMDB_API_KEY", "\"" + apiKey + "\""

    }
    buildTypes {
        debug{
            debuggable true
            testCoverageEnabled true
            applicationIdSuffix ".debug"

            buildConfigField "boolean", "LOG_NETWORK", "true"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "boolean", "LOG_NETWORK", "false"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // -- Support Libs
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'

    // -- Dagger 2
    apt 'com.google.dagger:dagger-compiler:2.0.2'
    compile 'com.google.dagger:dagger:2.0.2'
    // Needed for @Generated annotation (missing in Java <= 1.6; therefore, Android)
    // https://github.com/google/dagger/issues/95
    provided 'javax.annotation:jsr250-api:1.0'

    // -- Otto (Event Bus)
    compile 'com.squareup:otto:1.3.8'

    // -- Picasso Image
    compile 'com.squareup.picasso:picasso:2.5.2'
    // -- HTTP
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    // -- GSON
    compile 'com.google.code.gson:gson:2.4'


    // -- Android Tests
    testCompile 'junit:junit:4.12'
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile ('com.android.support.test:runner:0.3'){
        exclude module: 'support-annotations'
    }
    androidTestCompile ('com.android.support.test:rules:0.3'){
        exclude module: 'support-annotations'
    }

    testCompile "org.robolectric:robolectric:3.0"
    testCompile 'org.robolectric:shadows-support-v4:3.0'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'com.google.dexmaker:dexmaker:1.2'
    testCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
    testApt 'com.google.dagger:dagger-compiler:2.0.1'

    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestApt 'com.google.dagger:dagger-compiler:2.0.1'
}
